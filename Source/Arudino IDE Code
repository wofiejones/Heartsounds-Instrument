// Button and LED variables
const int buttonPins[] = {2, 3, 4, 5, 6, 7, 8, 9};
const int ledPin = 13;
int lastButtonStates[] = {HIGH, HIGH, HIGH, HIGH, HIGH, HIGH, HIGH, HIGH};
int ledState = LOW;

// MIDI variables
int controls[] = {21, 22, 23, 24, 25, 26, 27, 28};
// int channels[] = {1, 2, 3, 4, 5, 6, 7, 8};

void setup() {
  for (int i = 0; i < 8; i++) {
    pinMode(buttonPins[i], INPUT_PULLUP);
  }
  pinMode(ledPin, OUTPUT);

  Serial.begin(9600);
}

void loop() {
  for (int i = 0; i < 8; i++) {
    // checkButtonStatus(buttonPins[i], lastButtonStates[i], controls[i], channels[i]);
    checkButtonStatus(buttonPins[i], lastButtonStates[i], controls[i], 1);
  }
}

void checkButtonStatus(int pin, int &lastState, int control, int channel) {
  int buttonState = digitalRead(pin);

  if (buttonState != lastState) {
    lastState = buttonState;

    // Active Low
    if (buttonState == LOW) {
      usbMIDI.sendControlChange(control, 127, channel);
      digitalWrite(ledPin, HIGH);
      Serial.print("Note playing from button assigned to controller ");
      Serial.println(control);
    } else {
      usbMIDI.sendControlChange(control, 0, channel);
      digitalWrite(ledPin, LOW);
    }
    delay(50); // Debounce delay
  }
}
